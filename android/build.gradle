// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
          classpath 'com.android.tools.build:gradle:3.5.0'
    }
}

apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION             = 28
def DEFAULT_BUILD_TOOLS_VERSION             = "28.0.3"
def DEFAULT_TARGET_SDK_VERSION              = 28
def DEFAULT_FIREBASE_ANALYTICS_VERSION      = "17.2.3"
def DEFAULT_FIREBASE_AUTH_VERSION           = "19.2.0"
def DEFAULT_SUPPORT_LIB_VERSION             = "28.0.0"



android {
    compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
    buildToolsVersion rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$projectDir/../../../node_modules/react-native/android"
    }
    google()
}

dependencies {
    //noinspection GradleCompatible
    def firebaseAnalyticsVersion = rootProject.hasProperty('firebaseAnalyticsVersion')  ? rootProject.firebaseAnalyticsVersion : DEFAULT_FIREBASE_ANALYTICS_VERSION
    def firebaseAuthVersion = rootProject.hasProperty('firebaseAuthVersion')  ? rootProject.firebaseAuthVersion : DEFAULT_FIREBASE_AUTH_VERSION
    def supportLibVersion =  rootProject.hasProperty('supportLibVersion')  ? rootProject.supportLibVersion : DEFAULT_SUPPORT_LIB_VERSION
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.facebook.react:react-native:+"
    implementation "com.google.firebase:firebase-analytics:$firebaseAnalyticsVersion"
    implementation "com.google.firebase:firebase-auth:$firebaseAuthVersion"
}
